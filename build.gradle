buildscript {
    ext.kotlin_version = '1.8.22'
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.quarkus:gradle-application-plugin:3.1.1.Final"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-allopen'
    apply plugin: "idea"

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
        implementation "jakarta.enterprise:jakarta.enterprise.cdi-api"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    }

    allOpen {
        annotation("jakarta.ws.rs.Path")
        annotation("jakarta.enterprise.context.ApplicationScoped")
        annotation("io.quarkus.test.junit.QuarkusTest")
    }

    kotlin {
        jvmToolchain(11)
    }
}

project(":application:api") {
}

project(":application:usecases") {
    dependencies {
        implementation project(":application:api")
        implementation project(":application:domain")

        implementation "jakarta.transaction:jakarta.transaction-api"
    }
}

project(":application:domain") {
}

project(":infrastructure:web") {
    dependencies {
        implementation project(":application:api")
        implementation "io.quarkus:quarkus-resteasy-jackson"
        implementation "io.quarkus:quarkus-smallrye-graphql"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    }
}

project(":infrastructure:persistence") {
    dependencies {
        implementation project(":application:domain")
        implementation "io.quarkus:quarkus-hibernate-orm"
        implementation "io.quarkus:quarkus-jdbc-h2"
        implementation "io.quarkus:quarkus-flyway"
    }
}

project(":main-partition") {
    apply plugin: "io.quarkus"

    dependencies {
        implementation project(":application:api")
        implementation project(":application:usecases")
        implementation project(":application:domain")
        implementation project(":infrastructure:persistence")
        implementation project(":infrastructure:web")
        implementation "io.quarkus:quarkus-resteasy"
        implementation "io.quarkus:quarkus-kotlin"
        implementation 'io.quarkus:quarkus-container-image-docker'
    }
}